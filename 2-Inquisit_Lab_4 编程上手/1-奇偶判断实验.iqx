// 今天咱们编写第一个实验程序：奇偶判断任务。实验的主要内容是让被试看到屏幕上呈现的一个数字，并判断是奇数还是偶数，如果是奇数就点击键盘按键"F"，如果是偶数就点击键盘按键"J"。记录被试的反应时。

**************************************************************************************************************************************************************************
// 我们按照自下而上的编程思路，首先要想好我们准备呈现哪些内容。根据我们当前的实验要求，我们需要呈现给被试的是数字，包括奇数和偶数，我们暂且定义为1-8,奇数为1,3,5,7；偶数为2,4,6,8。

<item oddnumber>
	/1 = "1"
	/2 = "3"
	/3 = "5"
	/4 = "7"
</item>

<item evennumber>
	/1 = "2"
	/2 = "4"
	/3 = "6"
	/4 = "8"
</item>

// 定义好需要呈现的条目之后，我们要考虑，这些item是需要按照文字的方式呈现，那么我们通过"<text></text>"的方式，定义一下以何种形式呈现

<text oddnumbertxt>
	/items = oddnumber
	/ fontstyle = ("Arial",40ptc)
	/txbgcolor = (0,0,0)
	/txcolor = (255,255,255)
</text>

<text evennumbertxt>
	/items = evennumber
	/ fontstyle = ("Arial",40ptc)
	/txbgcolor = (0,0,0)
	/txcolor = (255,255,255)
</text>
**************************************************************************************************************************************************************************
// 现在，我们已经定义好了需要呈现的内容，我们接下来要把需要呈现的内容添加到每个trial里，形成一个完整的试次。对于奇数和偶数，被试的正确反映选项是不一样的，因此，我们需要两个trial来呈现。

<trial oddnumbertrial>
	/pretrialpause = 500
	/validresponse = ("F","J")
	/correctresponse = ("F")
	/stimulusframes = [1 = oddnumbertxt]
</trial>

<trial evennumbertrial>
	/pretrialpause = 500
	/validresponse = ("F","J")
	/correctresponse = ("J")
	/stimulusframes = [1 = evennumbertxt]
</trial>

**************************************************************************************************************************************************************************
// 现在每个试次都编辑好了，我们可以把试次组织到区组模块当中，方便实验调用,并返回相应的结果值,最终，我们将block 整合到expt模块中，加在到整个实验当中。

<block oddevenblk>
	/screencolor = (0,0,0)
	/blockfeedback = (meanlatency,correct)
	/trials = [1-16 = noreplace(oddnumbertrial, evennumbertrial)]
</block>


<expt>
	/ blocks = [1 = oddevenblk]
</expt>

**************************************************************************************************************************************************************************
// 至此，我们的第一个实验就完成了，在这个实验中，首先可以自上而下的构思:
// 1. 我们要完成一个实验，实验包含一个区组;
// 2. 区组中设定背景颜色，完成区组后呈现的反馈结果，区组中包含的试次数量和类型。这个区组需要呈现中包含16个试次，分为两种类型，分别是奇数试次和偶数试次，通过随机的方式反复调用8次，顺序随机；
// 3. 我们需要定义两种试次，分别是奇数和偶数试次。在每个试次中设定呈现的事件序列，有效的键盘反应，正确的反应键，试次呈现的内容。其中内容是调用编辑好的刺激。
// 4. 在刺激中，我们定义为文字类型的刺激，定义刺激呈现的字体，大小，颜色，背景颜色等信息，最重要的是，呈现的内容来源是调用编辑好的条目，也就是最基本的需要呈现的内容。
// 5. 我们需要提前编辑好我们要呈现的内容，也就是奇数和偶数，我们需要用item进行定义，定义为1个列表。

// 在实验的具体编制过程中，我们一般采用自下而上的思路进行编程，也就是先把需要呈现的内容条目编辑好，然后下一步编辑刺激，之后编辑试次，区组，最终组织到实验当中。

// 通过这样的步骤，我们就完成了我们第一个实验的编制。






















